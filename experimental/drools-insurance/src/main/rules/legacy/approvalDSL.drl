#created on: 23/05/2006
package org.acme.insurance
expander acme.dsl

#
# These rules are mostly "negative" in the sense that
# a driver has to not be rejected. There is 
# one rule that allows immediate approval however.
#

rule "Driver has had too many accidents"
	
	when 
		There is a Driver
			- has had more than 3 prior claims
	then 
		Reject Policy with explanation : 'Too many accidents'
end 


rule "Driver is 45"
	
	when 
		There is a Driver
			- age is equal to 45 years old
	then 
		Reject Policy with explanation : 'Too old'
end 


rule "Driver is underage"

	when
		There is a Driver
			- age less than 18 years old
	then 		
		Reject Policy with explanation : 'Driver is underage. Try another insurer'
		
end

rule "Driver is in marginal age" 	
	when
		There is a Driver
			- age is between 18 and 24 years old
			- has had more than 1 prior claims
		Policy type is 'COMPREHENSIVE'
	then
		Reject Policy with explanation : 'No accidents allowed if in marginal age group'
end

rule "Driver in unsafe area for marginal age"
	when
		Policy type is 'COMPREHENSIVE'	
		There is a Driver
			- age less than 25 years old
			- has a location risk profile of 'HIGH'				
	then
		Reject Policy with explanation : 'Marginal age driver in high risk area'		
end

rule "Driver in unsafe area with priors"
	when
		There is a Driver
			- has a location risk profile of 'MED'
			- age less than 25 years old
			- has had more than 1 prior claims
		Policy type is 'COMPREHENSIVE'
		
	then
		Reject Policy with explanation : 'Driver in that area is too risky - given past accidents and age.'
end

rule "Driver unsafe for third party"
	when
		Policy type is 'THIRD_PARTY'		
		There is a Driver
			- has had more than 2 prior claims
	then		
		Reject Policy with explanation : 'Too many priors for third party'
		
end
 
rule "Driver in bad area for theft"
	when 
		Policy type is 'FIRE_THEFT'
		There is a Driver
			- has a location risk profile of 'HIGH'
	then		
		Reject Policy with explanation : 'Unsafe area for theft'				
end

rule "Quick approval - safe driver, any policy type"
	when		
		Policy has not been rejected	
		There is a Driver
			- age is at least 30
			- has had exactly 0 prior claims
	then
		Approve Policy with the reason : 'Driver is safe and mature.'
end

