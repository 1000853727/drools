#created on: 7.6.2007
package org.drools.analytics.rangeChecks.patterns;

#list any import classes here.
import org.drools.analytics.components.*;
import org.drools.analytics.accumulateFunction.ValidatePattern;
import org.drools.analytics.accumulateFunction.ValidatePatternResult;
import org.drools.analytics.result.AnalysisResult;
import org.drools.analytics.result.MissingNumberPattern;
import org.drools.analytics.result.Cause;
import org.drools.analytics.result.AnalysisWarning;

import java.util.ArrayList;
import java.util.List;

#declare any global variables here
global AnalysisResult result

#
# If all pattern ranges are not checked for a field.
# 
# Type: Warning
# Example: in "Rule 1" Foo( bar == 10 ) and in "Rule 2" Foo( bar == 20 ) and in "Rule 3" Foo( bar == 40 )
# 					then Foo( bar == 30 ) is missing.
rule "Range check for number patterns"
	when
		$f :Field( ( fieldType == Field.FieldType.INT || == Field.FieldType.DOUBLE ), 
					$fieldId :id, $fieldName :name, $classId :classId )
		(
			# Where pattern is false.
			$validationResult :ValidatePatternResult( value != null)
				from accumulate(
					$r :LiteralRestriction( 
						fieldId == $fieldId, 
						patternIsNot == false, 
						( evaluator == "==" || == "!=" ),
						$evaluator :evaluator
					),
					validatePattern( $r )
				)
		) or (
			# Where pattern is true.
			$validationResult :ValidatePatternResult( value != null)
				from accumulate(
					$r :LiteralRestriction( 
						fieldId == $fieldId, 
						patternIsNot == true, 
						( evaluator == "==" || == "!=" ),
						$evaluator :evaluator
					),
					validatePattern( $r )
				)
		)
	then
		List<Cause> list = new ArrayList<Cause>();
		list.add(
				new MissingNumberPattern( $f.getId(), "==", $validationResult.getValue().toString() ));
		
		result.add( new AnalysisWarning( $f.getRuleName(), $f + " is missing a number from pattern.", list ) );
end
