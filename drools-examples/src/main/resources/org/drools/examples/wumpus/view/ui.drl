package org.drools.examples.wumpus.view

import org.drools.examples.wumpus.Move
import org.drools.examples.wumpus.Cell

import org.drools.examples.wumpus.Wumpus
import org.drools.examples.wumpus.Pit
import org.drools.examples.wumpus.Gold
import org.drools.examples.wumpus.Hero
import org.drools.examples.wumpus.SeeGlitter
import org.drools.examples.wumpus.SmellStench
import org.drools.examples.wumpus.FeelBreeze
import org.drools.examples.wumpus.FeelBump
import org.drools.examples.wumpus.HearScream
import org.drools.examples.wumpus.WumpusDeath
import org.drools.examples.wumpus.PitDeath
import org.drools.examples.wumpus.GoldWin
import org.drools.examples.wumpus.Reset
import org.drools.examples.wumpus.Score
import org.drools.examples.wumpus.view.GameView

import org.drools.runtime.StatefulKnowledgeSession
import org.drools.logger.KnowledgeRuntimeLogger
import org.drools.logger.KnowledgeRuntimeLoggerFactory

import javax.swing.JOptionPane

import java.util.ArrayList

dialect "mvel"
  

declare Cell
	@propertySpecific
end	

declare Hero
	@propertySpecific
end	

declare Gold
	@propertySpecific
end	

declare Wumpus
    @propertySpecific
end    

rule "Create GUI" when
    not GameUI()
then
    insert( new GameUI( ) );     
end          

rule "Reset View" when
    Reset()
    $gv : GameView()
then
    retract( $gv );
end

rule "Init Game View" salience -200 when
    $ui : GameUI()
    not Reset()
    not GameView()
    $w : Wumpus()
    $g : Gold()
    $h : Hero()    
then    
    GameView $gv = new GameView();
    $gv.init($w, $g, $h, 50, 50, 3, 20, 5, 5);
    $gv.setKsession( (StatefulKnowledgeSession) kcontext.knowledgeRuntime );
    $gv.setKbase( ((StatefulKnowledgeSession) kcontext.knowledgeRuntime).knowledgeBase );
    
    new java.io.File("log").mkdir();
    KnowledgeRuntimeLogger klogger = KnowledgeRuntimeLoggerFactory.newFileLogger((StatefulKnowledgeSession) kcontext.knowledgeRuntime, "log/wumpus");   
    $gv.setKlogger( klogger );   
     $ui.gameView = $gv; 
    insert( $gv );
end    

rule "Score Updated" when
    $gv : GameView()
    $s : Score()
then
    channels["score"].send( $s );
end

rule "Wumpus Death View" dialect "java"   when
          WumpusDeath() 
    $g  : GameView()          
then
	JOptionPane.showMessageDialog( null,
                                   "Oh No! You where killed by the Wumpus" );
    insert( new Reset() );   
end

rule "Pit Game View" dialect "java" when
         PitDeath()
    $g : GameView()
then    
	JOptionPane.showMessageDialog( null,
                                   "Oh No! You where killed by a Pit" );
    insert( new Reset() );  
end

rule "Gold Win View" dialect "java"  when
		 GoldWin()
    $g : GameView()
then
	JOptionPane.showMessageDialog( null,
                                   "Congratulations! You retreived the gold from the cave" );
    insert( new Reset() );  
end