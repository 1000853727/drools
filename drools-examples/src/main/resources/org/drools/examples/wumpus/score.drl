package org.drools.examples.wumpus

import org.drools.examples.wumpus.Score

dialect "mvel"

declare Score
	@propertySpecific
end	

rule "Gold Win +100" salience 200 when
         Reset()    
         GoldWin()
    $s : Score() @watch(!*)
then
    modify( $s ) { value = $s.value  + 100 };
end    
    
    
rule "Move -1" salience 200 when
         MoveCommand()
    $s : Score() @watch(!*)
then
    modify( $s) { value = $s.value  - 1 };  
end  

rule "Shoot Arrow -10" salience 200 when
         ShootCommand()
    $s : Score() @watch(!*)
then
    modify( $s) { value = $s.value  - 10 };
end   
        
rule "Pit Death -1000" salience 200 when
         PitDeath()
    $s : Score() @watch(!*)
then
    modify( $s) { value = $s.value  - 1000 };
end 

rule "Wumpus Death -1000" salience 200 when
         WumpusDeath()
    $s : Score() @watch(!*)
then
     modify( $s) { value = $s.value - 1000 };  
end          

