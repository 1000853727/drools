package org.drools.examples.wumpus

import org.drools.examples.wumpus.Move
import org.drools.examples.wumpus.Cell

import org.drools.examples.wumpus.Wumpus
import org.drools.examples.wumpus.Pitt
import org.drools.examples.wumpus.Gold
import org.drools.examples.wumpus.SeeGlitter
import org.drools.examples.wumpus.SmellStench
import org.drools.examples.wumpus.FeelBreeze

dialect "mvel"

rule "Smell Stench"  when 
    $s  : Sensors()
    $h  : Hero()
          Wumpus(row == ($h.row + 1), col == $h.col ) or
          Wumpus(row == ($h.row - 1), col == $h.col ) or          
          Wumpus(row == $h.row, col == ($h.col + 1) ) or
          Wumpus(row == $h.row, col == ($h.col - 1) )
then
    insertLogical( new SmellStench() );
    $s.smellStench = true;
end

rule "Stench Gone" when
    $s  : Sensors()
    not SmellStench()
then
    $s.smellStench = false;
end    

rule "Feel Breeze"  when 
    $s  : Sensors()
    $h  : Hero()
          Pitt(row == ($h.row + 1), col == $h.col ) or
          Pitt(row == ($h.row - 1), col == $h.col ) or          
          Pitt(row == $h.row, col == ($h.col + 1) ) or
          Pitt(row == $h.row, col == ($h.col - 1) )
then
    insertLogical( new FeelBreeze() );
    $s.feelBreeze = true; 
end

rule "Breeze Gone" when
    $s  : Sensors()
    not FeelBreeze()
then
    $s.feelBreeze = false;
end 

rule "See Glitter"  when 
    $s  : Sensors()
    $h  : Hero()
          Gold(row == ($h.row + 1), col == $h.col ) or
          Gold(row == ($h.row - 1), col == $h.col ) or          
          Gold(row == $h.row, col == ($h.col + 1) ) or
          Gold(row == $h.row, col == ($h.col - 1) )
then
    insertLogical( new SeeGlitter() );
    $s.seeGlitter = true; 
end

rule "Glitter Gone" when
    $s  : Sensors()
    not SeeGlitter()
then
    $s.seeGlitter = false;
end 