package org.drools.games.pong

import org.drools.games.pong.Ball
import org.drools.games.pong.Bat
import org.drools.games.pong.Run
import org.drools.games.pong.Player
import org.drools.games.pong.PlayerId
import org.drools.games.pong.PongGame
import org.drools.games.pong.PointWin
import org.drools.games.pong.KeyPressed
import org.drools.games.pong.Collision
import org.drools.games.pong.CollisionType

import java.lang.Math

import java.awt.Color
import java.awt.Graphics
import java.awt.event.KeyEvent

global PongConfiguration pconf

dialect "mvel"


rule "Move Ball" agenda-group "Move" when
    ui : PongUI( )
    ball : Ball()
    $r : Run()
then
   ui.restoreBallBackground(ball);
   modify( ball ) { x =  ball.x + ball.dx, y =  ball.y + ball.dy };
   ui.drawBall(ball);
   //System.out.println( "delta: " + ball.dx + " : " + ball.dy );
end

rule "Move Bat Up"  agenda-group "Move" when
    ui : PongUI( )
    ( ( KeyPressed( keyText == "A" ) and  bat : Bat( playerId == PlayerId.PlayerOne ) )
       or
      (KeyPressed( keyText == "K" ) and  bat : Bat( playerId == PlayerId.PlayerTwo ) )
    )
    Run()
    if ( bat.y -4 <= (pconf.padding + pconf.sideLineWidth ) ) break[bounded]
then
    ui.restoreBatBackground(bat);
    modify( bat ) { y = bat.y - 4  };
    ui.drawBat(bat);
then[bounded]
    ui.restoreBatBackground(bat);
    modify( bat ) { y = pconf.padding + pconf.sideLineWidth };
    ui.drawBat(bat);
end

rule "Move Bat Down"  agenda-group "Move" when
    ui : PongUI( )
    ( ( KeyPressed( keyText == "Z" ) and  bat : Bat( playerId == PlayerId.PlayerOne ) )
       or
      (KeyPressed( keyText == "M" ) and  bat : Bat( playerId == PlayerId.PlayerTwo ) )
    )
    Run()
    if ( bat.y +4 >= (pconf.tableHeight - pconf.padding - pconf.sideLineWidth - pconf.batHeight )) break[bounded]
then
    ui.restoreBatBackground(bat);
    modify( bat ) { y = bat.y + 4 };
    ui.drawBat(bat);
then[bounded]
    ui.restoreBatBackground(bat);
    modify( bat ) { y =  pconf.tableHeight- pconf.padding - pconf.getSideLineWidth()- pconf.batHeight  };
    ui.drawBat(bat);
end
