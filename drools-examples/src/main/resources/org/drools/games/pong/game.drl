package org.kie.games.pong

import org.kie.games.pong.Ball
import org.kie.games.pong.Bat
import org.kie.games.pong.Player
import org.kie.games.pong.PlayerId
import org.kie.games.pong.PongGame
import org.kie.games.pong.PointWin
import org.kie.games.pong.KeyPressed
import org.kie.games.pong.Collision
import org.kie.games.pong.CollisionType

import java.lang.Math

import org.kie.runtime.StatefulKnowledgeSession
import org.kie.logger.KnowledgeRuntimeLogger

import java.awt.Color
import java.awt.Graphics
import java.awt.event.KeyEvent

global PongConfiguration pconf

dialect "mvel"  

rule "PlayerOne Win" when
    ui : PongUI( ready == true )
    td : TableDirty( )    
    ball : Ball( x >= pconf.tableWidth + width )
    player : Player( id == PlayerId.PlayerOne )    
then
    tableG = ui.getTablePanel().getTableG();
    
    copyX = 0 - (pconf.tableWidth - ball.x);
    copyY = ball.y;

    tableG.copyArea( pconf.tableWidth, 0,
                     ball.width, ball.width,
                     copyX,copyY );  
                     
    retract( ball );
    modify( player ) { score = player.score + 1 };
    insert( new PointWin( player ) );
end

rule "PlayerTwo Win" when
    ui : PongUI( ready == true )
    td : TableDirty( )    
    ball : Ball( x <= 0 )
    player : Player( id == PlayerId.PlayerTwo )
then
    tableG = ui.getTablePanel().getTableG();
    
    copyX = 0 - (pconf.tableWidth - ball.x);
    copyY = ball.y;

    tableG.copyArea( pconf.tableWidth, 0,
                     ball.width, ball.width,
                     copyX,copyY );  
                     
    retract( ball );
    modify( player ) { score = player.score + 1 };  
    insert( new PointWin( player ) );
end

rule "Remove PointWin" salience -25 when
    pw : PointWin()
then
    retract( pw );
end