package org.drools.examples;

import org.drools.examples.GolfingExample.Golfer;

rule "find solution"
    when
        // There is a golfer named Fred, 
        // Whose positions is $p1
        $fred : Golfer( $fredsName : name == "Fred", 
                $fredsPosition : position, 
                $fredsColor : color  )

        // Joe is in position 2  		          
        $joe : Golfer( $joesName : name == "Joe", 
                $joesPosition  : position == 2, 
                position != $fredsPosition,
                $joesColor : color != $fredsColor )

 		// Bob is wearing plaid pants        
        $bob : Golfer( $bobsName : name == "Bob", 
                $bobsPosition : position != $fredsPosition,
                position != $joesPosition,                                                  
                $bobsColor : color == "plaid",
                color != $fredsColor,
                color != $joesColor )
 		  		
        // Tom isn't in position 1 or 4
        // and isn't wearing orange	
        $tom : Golfer( $tomsName : name == "Tom", 
                $tomsPosition : position != 1,
                position != 4,
                position != $fredsPosition,
                position != $joesPosition, 
                position != $bobsPosition,                                
                $tomsColor : color != "orange",
                color != "blue",
                color != $fredsColor,
                color != $joesColor,
                color != $bobsColor )           
                
  		// The golfer to Fred's immediate right
  		// is wearing blue pants
        $fn : Golfer( position == ( $fredsPosition + 1 ),
                      color == "blue",
                      this in ( $joe, $bob, $tom ) )

	then
        System.out.println( "Fred " + $fredsPosition + " " + $fredsColor );
        System.out.println( "Joe " + $joesPosition + " " + $joesColor );
        System.out.println( "Bob " + $bobsPosition + " " + $bobsColor );
        System.out.println( "Tom " + $tomsPosition + " " + $tomsColor );   
end    
