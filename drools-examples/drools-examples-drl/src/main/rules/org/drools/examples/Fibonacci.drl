package org.drools.examples

import org.drools.examples.FibonacciExample.Fibonacci;

rule Recurse
	salience 10
	when
		f : Fibonacci ( value == -1 )
	then
		insert( new Fibonacci( f.getSequence() - 1 ) );
		System.out.println( "recurse for " + f.getSequence() );
end

rule Bootstrap1
	salience 20
	when
		f : Fibonacci( sequence == 1, value == -1 )
	then 
		f.setValue( 1 );		
		update( f );
		System.out.println( f.getSequence() + " == " + f.getValue() );
end

rule Bootstrap2
	when
		f : Fibonacci( sequence == 2, value == -1 )
	then 
		f.setValue( 1 );		
		update( f );
		System.out.println( f.getSequence() + " == " + f.getValue() );		
end

rule Calculate
	when
		f1 : Fibonacci( s1 : sequence, value != -1 )
		f2 : Fibonacci( s2 : sequence == (s1 + 1 ), value != -1 )
 		f3 : Fibonacci( sequence == (s2 + 1 ), value == -1 )				
	then	
		f3.setValue( f1.getValue() + f2.getValue() );
		update( f3 );
		retract( f1 );
		System.out.println( f3.getSequence() + " == " + f3.getValue() );
end	
