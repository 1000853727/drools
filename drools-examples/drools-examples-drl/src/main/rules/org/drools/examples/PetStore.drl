package org.drools.examples

import org.drools.examples.PetStore.Order
import org.drools.examples.PetStore.Purchase
import org.drools.examples.PetStore.Product
import java.util.ArrayList
import javax.swing.JOptionPane;
        
global javax.swing.JFrame frame 
global javax.swing.JTextArea textArea

dialect "mvel"

// insert each item in the shopping cart into the Working Memory 
rule "Explode Cart"
    salience 10
	dialect "java"
	when
	    $order : Order()
		$item : Purchase() from $order.items
	then
		insert( $item );
end

// insert each item in the shopping cart into the Working Memory 
rule "Remove Explode Cart"
    salience 9
	dialect "java"
	when
	then
		drools.getWorkingMemory().getRuleBase().removeRule("org.drools.examples", "Explode Cart" );		
		drools.getWorkingMemory().getRuleBase().removeRule("org.drools.examples", "Remove Explode Cart" );				
end


// Free Fish Food sample when we buy a Gold Fish if we haven't already  bought 
// Fish Food and dont already have a Fish Food Sample
rule "Free Fish Food Sample"
    salience 8
	dialect "mvel"
	when
	    $order : Order()
		not ( $p : Product( name == "Fish Food") && Purchase( product == $p ) )
		not ( $p : Product( name == "Fish Food Sample") && Purchase( product == $p ) )		
		exists ( $p : Product( name == "Gold Fish") && Purchase( product == $p ) )	
		$fishFoodSample : Product( name == "Fish Food Sample" );
	then
		System.out.println( "Adding free Fish Food Sample to cart" );
		purchase = new Purchase($order, $fishFoodSample);
		insert( purchase );
		$order.addItem( purchase );	
end


// Suggest a tank if we have bought more than 5 gold fish and dont already have one
rule "Suggest Tank"
       dialect "java"
	when
	    $order : Order()
		not ( $p : Product( name == "Fish Tank") && Purchase( product == $p ) )	
		ArrayList( $total : size > 5 ) from collect( Purchase( product.name == "Gold Fish" ) )
		$fishTank : Product( name == "Fish Tank" )		
	then

 	
        Object[] options = {"Yes",
                            "No"};
                            
        int n = JOptionPane.showOptionDialog(frame,
                                             "Would you like to buy a tank for your " + $total + " fish?",
                                             "Purchase Suggestion",
                                             JOptionPane.YES_NO_OPTION,
                                             JOptionPane.QUESTION_MESSAGE,
                                             null,
                                             options,
                                             options[0]);
                                             
       System.out.print( "SUGGESTION: Would you like to buy a tank for your "
  	                     + $total + " fish? - " );

       if (n == 0) {
             Purchase purchase = new Purchase( $order, $fishTank );
             insert( purchase );
             $order.addItem( purchase );
           System.out.println( "Yes" );
       } else {
            System.out.println( "No" );
       }	
end	

rule "Gross Total"
    salience -10
    dialect "mvel"
    no-loop
	when
	    $order : Order( grossTotal == -1)
		Number( total : doubleValue ) from accumulate( Purchase( $price : product.price ) from $order.items,
   			        			   				 	   sum( $price ) )
	then	
	    $order.grossTotal = total;
	    textArea.append( "gross total=" + $order.grossTotal + "\n" );	    
	    update( $order );    
end

rule "Apply 5% Discount"
	dialect "mvel"
	when
		$order : Order( grossTotal >= 10 && < 20 )
	then
		$order.discountedTotal = $order.grossTotal * 0.95;
	    textArea.append( "discountedTotal total=" + $order.discountedTotal + "\n" );		
end


rule "Apply 10% Discount"
	dialect "mvel"
	when
		$order : Order( grossTotal >= 20 )
	then
		$order.discountedTotal = $order.grossTotal * 0.90;
	    textArea.append( "discountedTotal total=" + $order.discountedTotal + "\n" );	
end