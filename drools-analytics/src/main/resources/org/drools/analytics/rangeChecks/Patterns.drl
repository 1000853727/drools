#created on: 7.6.2007
package org.drools.analytics.rangeChecks.patterns;

#list any import classes here.
import org.drools.base.evaluators.Operator;

import org.drools.analytics.components.*;
import org.drools.analytics.dao.AnalyticsResult;
import org.drools.analytics.report.components.MissingNumberPattern;
import org.drools.analytics.report.components.Cause;
import org.drools.analytics.FindMissingNumber;

import java.util.ArrayList;
import java.util.List;

import org.drools.analytics.dao.AnalyticsResult;

#declare any global variables here
global AnalyticsResult result;

#
# If all pattern ranges are not checked for a field.
# 
# Type: Warning
# Depandencies: None
# Example: in "Rule 1" Foo( bar == 10 ) and in "Rule 2" Foo( bar == 20 ) and in "Rule 3" Foo( bar == 40 )
# 					then Foo( bar == 30 ) is missing.
rule "Range check for number patterns"
	when
		$f :Field(
			( fieldType == Field.FieldType.INT || == Field.FieldType.DOUBLE )
		)
		(
			# Where pattern is false.
			$list :ArrayList( size > FindMissingNumber.MIN_NUMBER_OF_RESTRICTIONS )
				from collect(
					LiteralRestriction( 
						fieldId == $f.id, 
						patternIsNot == false, 
						operator == Operator.EQUAL
					)
				)
		) or (
			# Where pattern is true.
			$list :ArrayList( size > FindMissingNumber.MIN_NUMBER_OF_RESTRICTIONS )
				from collect(
					LiteralRestriction( 
						fieldId == $f.id, 
						patternIsNot == true, 
						operator == Operator.EQUAL
					)
				)
		)
	then
		Number number = FindMissingNumber.testForPattern( $list );
		if( number != null ){
			MissingNumberPattern mnp = new MissingNumberPattern( $f, Operator.EQUAL, $f.getFieldType(), number.toString() );
			result.add( mnp );
		}
end

#
# If all pattern ranges are not checked for a field.
# 
# Type: Warning
# Depandencies: None
# Example: in "Rule 1" Foo( bar == 10 ) and in "Rule 2" Foo( bar == 20 ) and in "Rule 3" Foo( bar == 40 )
# 					then Foo( bar == 30 ) is missing.
rule "Range check for number patterns, !="
	when
		$f :Field( 
			( fieldType == Field.FieldType.INT || == Field.FieldType.DOUBLE ) 
		)
		(
			# Where pattern is false.
			$list :ArrayList( size > FindMissingNumber.MIN_NUMBER_OF_RESTRICTIONS )
				from collect(
					LiteralRestriction( 
						fieldId == $f.id, 
						patternIsNot == false, 
						operator == Operator.NOT_EQUAL
					)
				)
		) or (
			# Where pattern is false.
			$list :ArrayList( size > FindMissingNumber.MIN_NUMBER_OF_RESTRICTIONS )
				from collect(
					LiteralRestriction( 
						fieldId == $f.id, 
						patternIsNot == true, 
						operator == Operator.NOT_EQUAL
					)
				)
		)
	then
		Number number = FindMissingNumber.testForPattern( $list );
		if( number != null ){
			MissingNumberPattern mnp = new MissingNumberPattern( $f, Operator.NOT_EQUAL, $f.getFieldType(), number.toString() );
			result.add( mnp );
		}
end
