#created on: 28.1.2008
package org.drools.analytics.subsumption.patterns

import org.drools.base.evaluators.Operator;

import org.drools.analytics.components.LiteralRestriction;
import org.drools.analytics.components.Field.FieldType;
import org.drools.analytics.report.components.Subsumption;


#
# TODO:
#
rule "Find subsumptant restrictions, greater than"
	when
		# Covered x > 10
		$lower :LiteralRestriction(
			( valueType == Field.FieldType.INT || == Field.FieldType.DOUBLE || == Field.FieldType.DATE ),
			( operator == Operator.GREATER || == Operator.GREATER_OR_EQUAL )
		)

		# Covered x > 100
		$higher :LiteralRestriction( 
			fieldId == $lower.fieldId,
			( operator == Operator.GREATER || == Operator.GREATER_OR_EQUAL || == Operator.EQUAL)
		)
		
		eval( $lower.compareValues( $higher ) == -1 )
	then 
		insert( new Subsumption( $lower, $higher ) );
end

#
# TODO:
#
rule "Find subsumptant restrictions, less than"
	when
		# Covered x < 10
		$higher :LiteralRestriction(
			( valueType == Field.FieldType.INT || == Field.FieldType.DOUBLE || == Field.FieldType.DATE ),
			( operator == Operator.LESS || == Operator.LESS_OR_EQUAL )
		)

		# Covered x < 100
		$lower :LiteralRestriction( 
			fieldId == $higher.fieldId,
			( operator == Operator.LESS || == Operator.LESS_OR_EQUAL || == Operator.EQUAL)
		)
		
		eval( $lower.compareValues( $higher ) == - 1 )
	then 
		insert( new Subsumption( $higher, $lower ) );
end
