#created on: 7.9.2007
package org.drools.analytics.redundancy.rules

import org.drools.analytics.components.AnalyticsRule;
import org.drools.analytics.report.components.Redundancy;

#
# Only checks for redundant rule specifications, 
# does not include actual pattern and restriction checks.
#
rule "Find redundant Rules, weak redundancy"
	when
		# Check that AnalyticsRule $left and $right have redundant fields.
		$left :AnalyticsRule()
		$right :AnalyticsRule( 
			id != $left.id, 
			ruleAgendaGroup == $left.ruleAgendaGroup
		)
		
		exists Redundancy(
			left == $left.consequence,
			right == $right.consequence
		)
		
		# Check that there is not already a pair with these values.
		not Redundancy( 
			left == $left, 
			right == $right 
		)
		not Redundancy( 
			left == $right, 
			right == $left 
		)
	then
		insert( new Redundancy( Redundancy.RedundancyType.WEAK, $left, $right ) );
end

