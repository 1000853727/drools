#
# created on: 27.6.2008
#
# Rules in this package look for patterns and rules that are always satisfied.
#
#
package org.drools.verifier.alwaysTrue


import org.drools.verifier.components.Restriction;
import org.drools.verifier.components.Pattern;
import org.drools.verifier.components.PatternPossibility;

import org.drools.verifier.report.components.AlwaysTrue;
import org.drools.verifier.report.components.Opposites;
import org.drools.verifier.report.components.VerifierMessage;
import org.drools.verifier.report.components.Severity;
import org.drools.verifier.report.components.MessageType;

import org.drools.verifier.dao.VerifierResult;

global VerifierResult result;

#
# Finds pattern possibilities that are always true.
#
# Dependencies: None
#
# Example:
#		Pattern( a > 10 && a <= 10 )
#
rule "Pattern possibility that is always true"
	when
		$p :Pattern()
		
		$pp :PatternPossibility(
			patternId == $p.id
		)

		# For each restriction in pattern possibility 
		# there needs to be an opposite.
		forall(
			$r :Restriction(
				this memberOf $pp.items
			)
			
			Opposites( 
				(
					left == $r
				&&
					right memberOf $pp.items
				) || (
					left memberOf $pp.items
				&&
					right == $r
				)
			)
		)
	then 
		insert( new AlwaysTrue( $pp ));
end

#
# Finds patterns that are always true.
#
# Dependencies:
#	Needs at least one of the following packages or rules to fire:
# 		package org.drools.verifier.alwaysTrue;
#			rule "Pattern possibility that is always true"
#
# Example:
#		Pattern( a > 10 && a <= 10 )
#
rule "Pattern that is always true"
	when
		$p :Pattern()

		# For each restriction in each pattern possibility for current pattern, 
		# there needs to be an opposite.
		forall(
			$pp :PatternPossibility(
				patternId == $p.id
			)
			
			AlwaysTrue(
				cause == $pp
			)
		)
	then 
		result.add( new VerifierMessage(
								Severity.NOTE,
								MessageType.ALWAYS_TRUE,
								$p,
								$p + " is always satisfied." ) );
end