#created on: 7.9.2007
package org.drools.verifier.redundancy.patterns

import org.drools.verifier.components.*;
import org.drools.verifier.report.components.Redundancy;
import org.drools.verifier.report.components.RedundancyType;

#
# Only checks for redundant pattern specifications,
# does not include actual restriction checks.
#
# Dependencies: None
#
rule "Find redundant Patterns with restrictions"
	when
		# Check that Patterns $left and $right have redundant fields.
		$left :Pattern()
		$right :Pattern(
			guid not matches $left.guid,
	  		objectTypeGuid matches $left.objectTypeGuid,
	  		# TODO: In some cases the source might be redundant with different sources.
	 		sourceType == $left.sourceType,
	  		sourceGuid == $left.sourceGuid,
	  		patternNot == $left.patternNot,
	  		patternExists == $left.patternExists,
	  		patternForall == $left.patternForall
		)

		# Has possibilities, if the there is no possibilities,
		# then the Redundancy type is STRONG.
		# Because these patterns have no restrictions.
		exists SubPattern( patternGuid matches $left.guid )
		exists SubPattern( patternGuid matches $right.guid )

		# Check that there is not already a pair with these values.
		not Redundancy(
			left == $left,
			right == $right
		)
		not Redundancy(
			left == $right,
			right == $left
		)
	then
		insert( new Redundancy( RedundancyType.WEAK, $left, $right ) );
end

#
# Checks for redundant pattern specifications when the patterns have no possibilities.
#
# Dependencies: None
#
rule "Find redundant Patterns without restrictions"
	when
		# Check that Patterns $left and $right have redundant fields.
		$left :Pattern()
		$right :Pattern(
			guid not matches $left.guid,
	  		objectTypeGuid == $left.objectTypeGuid,
	  		# TODO: In some cases the source might be redundant with different sources.
	 		sourceType == $left.sourceType,
	  		sourceGuid == $left.sourceGuid,
	  		patternNot == $left.patternNot,
	  		patternExists == $left.patternExists,
	  		patternForall == $left.patternForall
		)

		# Not possibilities
		not SubPattern( patternGuid matches $left.guid )
		not SubPattern( patternGuid matches $right.guid )

		# Check that there is not already a pair with these values.
		not Redundancy(
			left == $left,
			right == $right
		)
		not Redundancy(
			left == $right,
			right == $left
		)
	then
		insert( new Redundancy( RedundancyType.STRONG, $left, $right ) );
end
